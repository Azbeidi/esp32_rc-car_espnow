#include <WiFi.h>
#include <esp_now.h>

//L298N wiring config
#define ENA 14 // motor A speed using PWM
#define ENB 32 // motor B speed using PWM
#define IN1 27 // motor A dir1
#define IN2 26 // motor A dir2
#define IN3 25 // motor B dir1
#define IN4 33 // motor B dir2

//Packet coming from transmitter
typedef struct __attribute__((packed)) {
  int16_t x; 
  int16_t y;  
} JoystickMsg;

JoystickMsg rxJoy = {0, 0};
uint32_t lastPacketMs = 0;

const int DEADZONE = 100; // joystick deadzone
const int RAW_MIN = -1000; // expected joystick range from TX values
const int RAW_MAX =  1000;
const int PWM_MIN = -255;
const int PWM_MAX =  255;
const uint32_t FAILSAFE_MS = 500;// stop if no packet in this time

void stopMotors() {
  digitalWrite(IN1, LOW);
  digitalWrite(IN2, LOW);
  digitalWrite(IN3, LOW);
  digitalWrite(IN4, LOW);
  analogWrite(ENA, 0);
  analogWrite(ENB, 0);
}

void commandMotor(int pwmPin, int dirPin1, int dirPin2, int speed) {
  speed = constrain(speed, PWM_MIN, PWM_MAX);

  //when moving the joystick sends signal to the LN298N motor
  if (speed > 0) {
    digitalWrite(dirPin1, HIGH);
    digitalWrite(dirPin2, LOW);
    analogWrite(pwmPin, speed);
  } else if (speed < 0) {
    digitalWrite(dirPin1, LOW);
    digitalWrite(dirPin2, HIGH);
    analogWrite(pwmPin, -speed);
  } else {
    digitalWrite(dirPin1, LOW);
    digitalWrite(dirPin2, LOW);
    analogWrite(pwmPin, 0);
  }
}

void applyJoystick(int16_t x, int16_t y) {
  if (abs(x) < DEADZONE) x = 0;
  if (abs(y) < DEADZONE) y = 0;

  // tank drive mix: y = forward/back, x = turn
  long mA = (long)y + (long)x;   // Motor A
  long mB = (long)y - (long)x;   // Motor B

  mA = constrain(mA, RAW_MIN, RAW_MAX);
  mB = constrain(mB, RAW_MIN, RAW_MAX);

  int pwmA = map(mA, RAW_MIN, RAW_MAX, PWM_MIN, PWM_MAX);
  int pwmB = map(mB, RAW_MIN, RAW_MAX, PWM_MIN, PWM_MAX);

  commandMotor(ENA, IN1, IN2, pwmA);
  commandMotor(ENB, IN3, IN4, pwmB);
}

//ESP NOW receive callback
void onEspNowRecv(const esp_now_recv_info_t *info, const uint8_t *data, int len) {
  if (len != (int)sizeof(JoystickMsg)) {
    Serial.print("Unexpected packet size: ");
    Serial.println(len);
    return;
  }

  memcpy(&rxJoy, data, sizeof(rxJoy));
  lastPacketMs = millis();

  Serial.printf("RX joystick X=%d Y=%d\n", rxJoy.x, rxJoy.y);

  applyJoystick(rxJoy.x, rxJoy.y);
}

void setup() {
  Serial.begin(115200);

  pinMode(IN1, OUTPUT);
  pinMode(IN2, OUTPUT);
  pinMode(IN3, OUTPUT);
  pinMode(IN4, OUTPUT);
  pinMode(ENA, OUTPUT);
  pinMode(ENB, OUTPUT);
  stopMotors();

  WiFi.mode(WIFI_STA);
  WiFi.disconnect();

  if (esp_now_init() != ESP_OK) {
    Serial.println("ESP NOW init failed");
    while (true) delay(100);
  }

  esp_now_register_recv_cb(onEspNowRecv);
  Serial.println("ESP NOW receiver ready");
}

void loop() {
  if (millis() - lastPacketMs > FAILSAFE_MS) {
    stopMotors();
  }
}
